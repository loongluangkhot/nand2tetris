// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/b/RAM512.hdl

/**
 * Memory of 512 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    DMux8Way(in=load, sel=address[6..8], a=loadMsb0, b=loadMsb1, c=loadMsb2, d=loadMsb3, e=loadMsb4, f=loadMsb5, g=loadMsb6, h=loadMsb7);
    RAM64(in=in, load=loadMsb0, address=address[0..5], out=outMsb0);
    RAM64(in=in, load=loadMsb1, address=address[0..5], out=outMsb1);
    RAM64(in=in, load=loadMsb2, address=address[0..5], out=outMsb2);
    RAM64(in=in, load=loadMsb3, address=address[0..5], out=outMsb3);
    RAM64(in=in, load=loadMsb4, address=address[0..5], out=outMsb4);
    RAM64(in=in, load=loadMsb5, address=address[0..5], out=outMsb5);
    RAM64(in=in, load=loadMsb6, address=address[0..5], out=outMsb6);
    RAM64(in=in, load=loadMsb7, address=address[0..5], out=outMsb7);
    Mux8Way16(a=outMsb0, b=outMsb1, c=outMsb2, d=outMsb3, e=outMsb4, f=outMsb5, g=outMsb6, h=outMsb7, sel=address[6..8], out=out);
}